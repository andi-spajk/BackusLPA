

    .BNF

<grammar> ::= <productions>

<productions> ::= <rule> <productions>
                | ""

<rule> ::= <non-terminal> "::=" <rhs>
<rhs> ::= <symbol> <symbols> <alts>
<symbol> ::= <terminal> | <non-terminal>
<symbols> ::= <symbol> <symbols>
            | ""
<alts> ::= <alt> <alts>
         | ""
<alt> ::= '\n' <newlines> '|' <rhs>
        | '|' <rhs>
<newlines> ::= '\n' <newlines>
             | ""

<non-terminal> ::= '<' <identifier> '>'
<identifier> ::= <id-char> <id-chars>
<id-chars> ::= <id-char> <id-chars>
             | ""
<id-char> ::=  <alpha> | <digit> | '-'

<terminal> ::= ''' <char-literal> '''
             | '"' <string-literal> '"'

<char-literal> ::= <char> | '"' | <char-escape>
<char-escape> ::= "\'" | <escape>

<string-literal> ::= <string-char> <string-literal>
                   | ""
<string-char> ::= <char> | "'" | <string-escape>
<string-escape> ::= "\"" | <escape>

<escape> ::= "\\" | "\n" | "\r" | "\t" | "\b" | "\f" | "\v"
<char> ::= '`' | '-' | '=' | '[' | ']' | ';' | ',' | '.' | '/' | '!' | '~' | '_'
         | '+' | '{' | '}' | '|' | ':' | '<' | '>' | '?' | '@' | '#' | '$' | '%'
         | '^' | '&' | '*' | '(' | ')' | ' '
         | <alpha> | <digit>

<alpha> ::= 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K'
          | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V'
          | 'W' | 'X' | 'Y' | 'Z' | 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g'
          | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r'
          | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
<digit> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
