    .EBNF

grammar = {production}

production = nonterminal "=" rhs
rhs = term {term} {{'\n'} '|' term {term}}
term = factor
     | '(' rhs ')' [modifier]
     | '{' rhs '}'
     | '[' rhs ']'
factor = symbol [modifier]
modifier = '*' | '+' | '?'
symbol = terminal | nonterminal

nonterminal = alpha {alpha | digit | '_'}

terminal = ''' char_literal '''
         | '"' {string_char} '"'

char_literal = char | '"' | char_escape
char_escape = "\'" | escape

string_char = char | "'" | string_escape
string_escape = "\"" | escape

escape = "\\" | "\n" | "\r" | "\t" | "\b" | "\f" | "\v"
char = '`' | '-' | '=' | '[' | ']' | ';' | ',' | '.' | '/' | '!' | '~' | '_'
     | '+' | '{' | '}' | '|' | ':' | '<' | '>' | '?' | '@' | '#' | '$' | '%'
     | '^' | '&' | '*' | '(' | ')' | ' '
     | alpha | digit
alpha = 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L'
      | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X'
      | 'Y' | 'Z' | 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j'
      | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v'
      | 'w' | 'x' | 'y' | 'z'
digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

(*
modifiers can only go on lone symbols and parenthetical expressions because:

)*	; {}
)+
)?	; []

}*	; redundant
}+	; contradictory but can collapse to +
}?	; contradictory

]*	; contradictory
]+	; contradictory
]?	; redundant
*)
